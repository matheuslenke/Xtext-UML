grammar br.ufes.mdd.umltextual.UmlTextual with org.eclipse.xtext.common.Terminals

generate umlTextual "http://www.ufes.br/mdd/umltextual/UmlTextual"

Model:
	elements+=ModelElement*
;
	
ModelElement:
	Package | UseCaseDiagram
;
	
// <--- Diagrama de Pacotes --->
	
Package:
	(instantiable='instantiable')? type=('package' | 'subsystem') name=ID ('depends' 'on' parentPackage=[Package])? '{'
	domainSpecificTypes+=DomainSpecificType*
	elements+=Element*
	'}'
;

//Subsystem:
//	 'subsystem' name=ID '{'
//	domainSpecificTypes+=DomainSpecificType*
//	elements+=Element*
//	'}'
//;

// <--- Diagrama de Classes --->

Element:
	Class | AssociationConnector | Interface
;


Class:
	('@'stereotype=ID)?
	visibility=Visibility? 'class' name=ID ('extends' parentClass=[Class])? ('implements' interface+=[Interface] (',' interface+=[Interface])*)? '{'
	attributes+=Attribute*
	methods+=Method*
	'}'
;

Interface:
	('@'stereotype=ID)?
	visibility=Visibility? 'interface' name=ID ('extends' parentClass=[Class])? '{'
	attributes+=Attribute*
	'}'
;

Visibility:
	"public" | "private" | "protected" | "package"
;

Abstract:
	'abstract'
;

Active:
	'active'
;

AttributeType:
	name="string" | name="int"  | name="double" | name="boolean" |
	name="byte"   | name="char" | name="float"  | name="short" |
	name="long"   | domainType=[DomainSpecificType]
;

DomainSpecificType:
	'domaintype' name=ID '=' description=STRING
	// Lembrar de modificar para declarar no começo da classe os tipos específicos de domínio
;

Multiplicity:
	"1" | "0..1" | "0..*" | "*" | "1..*" | "0.."INT | "1.."INT | INT | INT".."INT
	// Da pra generalizar pra INT | '*' | INT".."(INT | '*')
;

Attribute:
	visibility=(Visibility)? 'attr' name=ID ':' type=AttributeType ("[" multiplicity=Multiplicity "]")?
;

Method:
	'abstract'? 'static'? visibility=(Visibility)? 'function' name=ID '(' parameters+=Parameter*  ')' '->' returnType=AttributeType
;

Parameter:
	name=ID ':' parameterType=AttributeType
;

// <---- Relações ---->

Unamed:
	"unnamed"	
;

Navigation:
	"navigable"
;

AssociationConnector:
	Association | Aggregation | Composition
;

Association:
	'undirected'? 'association' name= (ID | Unamed) '{'
		'from' navigation1=Navigation?  class1=[Class]'['multiplicity1=Multiplicity ']' 
		'to'   navigation2=Navigation?  class2=[Class]'['multiplicity2=Multiplicity ']'
		('with' 'associatedclass' class=[Class])?
	'}'
;

Aggregation:
	'aggregation' name= (ID | Unamed) '{'
		'from' class1=[Class]'['multiplicity1=Multiplicity ']'
		'to' navigation=Navigation? class2=[Class]'['multiplicity2=Multiplicity ']'
	'}'
;

Composition:
	'composition' name= (ID | Unamed) '{'
		'from' class1=[Class]'['multiplicity1=Multiplicity ']'
		'to' navigation=Navigation? class2=[Class]'['multiplicity2=Multiplicity ']'
	'}'
;

// <--- Gramática do diagrama de Casos de Uso --->

UseCaseDiagram:
	'usecase' 'diagram' name=ID '{'
		elements+=UseCaseElement*
	'}'
;

UseCaseElement:
	Actor | UseCase
;

Actor:
	(visibility=Visibility)? (abstract=Abstract)? (active=Active)? (business='business')? 
	'actor' name=ID ('extends' parentActor=Actor)? '{'
		('uses' useCases+=[UseCase] (','useCases+=[UseCase])*)*
	'}'
;

UseCase:
	'usecase' name=ID  ('includes' includedUseCase+=[UseCase])? ('extends' extendedUseCase+=[UseCase])? '=' description=STRING
	('{'
		('includes' includedUseCase+=[UseCase] (','includedUseCase+=[UseCase])*
		 'extends'  extendedUseCase+=[UseCase] (','extendedUseCase+=[UseCase])*
		|
		 'extends'  extendedUseCase+=[UseCase] (','extendedUseCase+=[UseCase])*
		 'includes' includedUseCase+=[UseCase] (','includedUseCase+=[UseCase])*)
	'}')?
;

